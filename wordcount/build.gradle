/*
 * Copyright (c) 2007-2013 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.concurrentinc.com/
 */

import java.text.SimpleDateFormat

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

ext.cascadingVersion = '2.6.3'
ext.hadoopVersion = '0.20.1-dev'

sourceSets {
  main {
    java.srcDir 'src/java'
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'http://conjars.org/repo/'
  }
  maven {
    url 'https://repository.apache.org/content/repositories/releases/'
  }
  maven {
      url 'http://maven-qubole.s3-website-us-east-1.amazonaws.com/maven/release'
  }
  maven {
      url 'http://maven-qubole.s3-website-us-east-1.amazonaws.com/maven/snapshot'
  }
}

dependencies {
  compile group: 'cascading', name: 'cascading-core', version: cascadingVersion
  compile group: 'cascading', name: 'cascading-xml', version: cascadingVersion
  compile group: 'cascading', name: 'cascading-hadoop', version: cascadingVersion
  compile group: 'com.qubole', name: 'hadoop-core', version: hadoopVersion

  // these runtime deps allow the sample app to be run in an ide

  runtime group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1'
  runtime group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.1'

  // hadoop deps
  runtime( group: 'com.qubole', name: 'hadoop-core', version: hadoopVersion ) {
    exclude group: 'ant'
    exclude group: 'junit'
  }

  runtime group: 'com.qubole', name: 'hadoop-test', version: hadoopVersion
  runtime group: 'commons-io', name: 'commons-io', version: '2.1'

  runtime group: 'javax.ws.rs', name: 'jsr311-api', version: '1.1.1'
}

jar {
  description = "Assembles a Hadoop ready jar file"
  doFirst {
    into( 'lib' ) {
      from configurations.compile
    }
  }

  manifest {
    attributes( "Main-Class": "${archivesBaseName}/Main" )
  }
}

task distCopy( type: Copy, dependsOn: jar ) {

  into "${buildDir}/dist/${archivesBaseName}"

  from 'README.TXT'
  from 'build.gradle'
  from( 'src' ) {into 'src'}
  from( 'data' ) {into 'data'}

  from "$buildDir/libs"

}

task dist( type: Tar, dependsOn: distCopy ) {

  compression = "GZIP"
  classifier = new SimpleDateFormat( "yyyyMMdd" ).format( new Date() )

  from "${buildDir}/dist/"
}
